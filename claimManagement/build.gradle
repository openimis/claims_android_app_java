import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'com.android.application'
apply plugin: 'com.apollographql.apollo'

static def getDate() {
    LocalDateTime date = LocalDateTime.now()
    DateTimeFormatter formatter = DateTimeFormatter.ofPattern('dd MMMM yyyy HH:mm').withLocale(Locale.UK)
    return date.format(formatter)
}

repositories {
    mavenCentral()

    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
}

android {
    compileSdkVersion 32
    buildToolsVersion '33.0.0'

    defaultConfig {
        applicationId "org.openimis.imisclaims"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 4
        versionName "2.0.1"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOGGING_ENABLED", "false"

            // Refer to https://developer.android.com/reference/android/util/Log for the log level values
            // Irrelevant if LOGGING_ENABLED = false
            buildConfigField "int", "CONSOLE_LOG_LEVEL", '8' //Assert
            buildConfigField "int", "FILE_LOG_LEVEL", '8' //Assert
        }
        debug {
            debuggable true
            buildConfigField "boolean", "LOGGING_ENABLED", "true"

            // Refer to https://developer.android.com/reference/android/util/Log for the log level values
            buildConfigField "int", "CONSOLE_LOG_LEVEL", '2' //Verbose
            buildConfigField "int", "FILE_LOG_LEVEL", '2' //Verbose
        }
    }

    flavorDimensions 'std'

    productFlavors.all {
        resValue "string", "app_name_claims", "openIMIS Claims"
        resValue "string", "ReleaseDateValue", getDate()
        buildConfigField "String", "API_BASE_URL", '"http://demo.openimis.org/"'
        buildConfigField "String", "REST_API_PREFIX", '"rest"'
        buildConfigField "String", "RAR_PASSWORD", '")(#$1HsD"'
        buildConfigField "String", "API_VERSION", '"3"'
        buildConfigField "boolean", "DISABLE_PRICE_EDITION", 'false'
        buildConfigField "boolean", "GENERATE_CLAIM_NUMBER", 'false'
    }

    productFlavors {
        demoProd {
            applicationIdSuffix ".demoProd"
            resValue "string", "app_name_claims", "Claims Demo"
            buildConfigField "String", "API_BASE_URL", '"http://demo.openimis.org/"'
            dimension 'std'
        }

        demoRelease {
            applicationIdSuffix ".demoRelease"
            resValue "string", "app_name_claims", "Claims Release"
            buildConfigField "String", "API_BASE_URL", '"https://release.openimis.org/"'
            dimension 'std'
        }

        chfDev {
            applicationIdSuffix ".chfDev"
            resValue "string", "app_name_claims", "Claims CHF"
            buildConfigField "String", "API_BASE_URL", '"http://chf-dev.swisstph-mis.ch/"'
            dimension 'std'
        }

        chfProd {
            applicationIdSuffix ".chfProd"
            resValue "string", "app_name_claims", "Claims CHF"
            buildConfigField "String", "API_BASE_URL", '"http://chf-imis.or.tz/"'
            buildConfigField "String", "REST_API_PREFIX", '"restapi"'
            dimension 'std'
        }
        mvDev {
            applicationIdSuffix ".mvDev"
            resValue "string", "app_name_claims", "Claims MV"
            buildConfigField "String", "API_BASE_URL", '"http://imis-mv.swisstph-mis.ch/"'
            dimension 'std'
        }
        bephaDev {
            applicationIdSuffix ".bephaDev"
            resValue "string", "app_name_claims", "Claims BEPHA"
            buildConfigField "String", "API_BASE_URL", '"http://149.210.235.40/devupgrade/"'
            dimension 'std'
        }
        niger {
            applicationIdSuffix ".niger"
            resValue "string", "app_name_claims", "Claims Niger"
            buildConfigField "String", "API_BASE_URL", '"https://niger23.bluesquare.org/"'
        }
        tchadDev {
            applicationIdSuffix ".tchadDev"
            buildConfigField "String", "API_BASE_URL", '"https://imis-tchad-dev.swisstph-mis.ch/"'
            resValue "string", "app_name_claims", "Claims TCHAD DEV"
            dimension 'std'
        }
        local {
            applicationIdSuffix ".local"
            resValue "string", "app_name_claims", "Claims Local"
            buildConfigField "String", "API_BASE_URL", '"http://10.0.2.2:35787/"'
            dimension 'std'
        }
        cli {
            applicationIdSuffix System.getenv("APPLICATION_ID_SUFFIX") ?: ".cli"
            resValue "string", "app_name_claims", System.getenv("CLI_APP_NAME") ?: "Claims CLI"
            buildConfigField "String", "API_BASE_URL", '"' + (System.getenv("API_BASE_URL") ?: 'http://10.0.2.2:35787/') + '"'
            dimension 'std'
        }
        mauritaniaTrain {
            applicationIdSuffix ".mauritaniaTrain"
            buildConfigField "String", "API_BASE_URL", '"https://formation.cnass-mauritanie.swisstph-mis.ch/"'
            resValue "string", "app_name_claims", "Claims CNASS TRAIN"
            dimension = 'std'
        }
        gambia {
            applicationIdSuffix ".gambia"
            buildConfigField "String", "API_BASE_URL", '"https://openimis.nhia.gm/"'
            resValue "string", "app_name_claims", "Claims Gambia"
            dimension = 'std'
            buildConfigField "boolean", "DISABLE_PRICE_EDITION", 'true'
            buildConfigField "boolean", "GENERATE_CLAIM_NUMBER", 'true'
        }
    }

    sourceSets {
        demoProd.java.srcDir 'src/demoProd/java'
        demoProd.res.srcDir 'src/demoProd/res'

        demoRelease.java.srcDir 'src/demoRelease/java'
        demoRelease.res.srcDir 'src/demoRelease/res'

        chfDev.java.srcDir 'src/chf/java'
        chfDev.res.srcDir 'src/chf/res'

        chfProd.java.srcDir 'src/chf/java'
        chfProd.res.srcDir 'src/chf/res'

        mvDev.java.srcDir 'src/mv/java'
        mvDev.res.srcDir 'src/mv/res'

        bephaDev.java.srcDir 'src/bepha/java'
        bephaDev.res.srcDir 'src/bepha/res'

        niger.java.srcDir 'src/niger/java'
        niger.res.srcDir 'src/niger/res'

        tchadDev.java.srcDir 'src/tchad/java'

        local.java.srcDir 'src/local/java'
        local.res.srcDir 'src/local/res'

        mauritaniaTrain.java.srcDir 'src/demoProd/java'

        cli.java.srcDir System.getenv("CLI_JAVA_DIR") ?: 'src/demoProd/java'
        cli.res.srcDir System.getenv("CLI_RES_DIR") ?: 'src/demoProd/res'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

apollo {
    service("service") {
        packageName.set("org.openimis.imisclaims")
    }
    customTypeMapping = [
            "Date" : "java.util.Date",
            "DateTime" : "java.util.Date",
            "Decimal": "java.lang.Double",
    ]
}


// Apply custom flavours
if(file('custom-flavours.gradle').exists()){
    apply from: 'custom-flavours.gradle'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.embarkmobile:zxing-android-minimal:1.2.1@aar'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    implementation('com.apollographql.apollo:apollo-runtime:2.5.14')
    implementation ('com.apollographql.apollo:apollo-android-support:2.5.14'){
        because("Apollo 3+ only works with Kotlin coroutines")
    }
    implementation group: 'com.squareup.picasso', name: 'picasso', version: '2.71828'
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '1.2.7'

}
