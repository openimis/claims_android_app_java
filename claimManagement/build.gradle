import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'com.android.application'

// Apply custom flavours

static def getDate() {
    LocalDateTime date = LocalDateTime.now()
    DateTimeFormatter formatter = DateTimeFormatter.ofPattern('dd MMMM yyyy HH:mm').withLocale(Locale.UK)
    return date.format(formatter)
}

repositories {
    mavenCentral()

    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "org.openimis.imisclaims"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 3
        versionName "2.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        useLibrary 'org.apache.http.legacy'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions 'std'

    productFlavors.all {
        buildConfigField "String", "APP_DIR", '"IMIS"'
        resValue "string", "app_name_claims", "openIMIS Claims"
        resValue "string", "ReleaseDateValue", getDate()
        buildConfigField "String", "API_BASE_URL", '"http://demo.openimis.org/rest/"'
        buildConfigField "String", "RAR_PASSWORD", '")(#$1HsD"'
        buildConfigField "String", "API_VERSION", '"3"'
    }

    productFlavors {
        demoProd {
            applicationIdSuffix ".demoProd"
            buildConfigField "String", "APP_DIR", '"IMIS-DEMOPROD"'
            resValue "string", "app_name_claims", "Claims Demo"
            buildConfigField "String", "API_BASE_URL", '"http://demo.openimis.org/rest/"'
            dimension 'std'
        }

        demoRelease {
            applicationIdSuffix ".demoRelease"
            buildConfigField "String", "APP_DIR", '"IMIS-DEMORELEASE"'
            resValue "string", "app_name_claims", "Claims Release"
            buildConfigField "String", "API_BASE_URL", '"https://release.openimis.org/rest/"'
            dimension 'std'
        }

        chfDev {
            applicationIdSuffix ".chfDev"
            resValue "string", "app_name_claims", "Claims CHF"
            buildConfigField "String", "APP_DIR", '"IMIS-CHFDEV"'
            buildConfigField "String", "API_BASE_URL", '"http://chf-dev.swisstph-mis.ch/rest/"'
            dimension 'std'
        }
        mvDev {
            applicationIdSuffix ".mvDev"
            buildConfigField "String", "APP_DIR", '"IMIS-MVDEV"'
            resValue "string", "app_name_claims", "Claims MV"
            buildConfigField "String", "API_BASE_URL", '"http://imis-mv.swisstph-mis.ch/rest/"'
            dimension 'std'
        }
        bephaDev {
            applicationIdSuffix ".bephaDev"
            buildConfigField "String", "APP_DIR", '"IMIS-BEPHADEV"'
            resValue "string", "app_name_claims", "Claims BEPHA"
            buildConfigField "String", "API_BASE_URL", '"http://149.210.235.40/devupgrade/rest/"'
            dimension 'std'
        }
        niger {
            applicationIdSuffix ".niger"
            buildConfigField "String", "APP_DIR", '"IMIS-NIGER"'
            resValue "string", "app_name_claims", "Claims Niger"
            buildConfigField "String", "API_BASE_URL", '"http://oiniger.bluesquare.org/rest/"'
		}
        tchadDev {
            applicationIdSuffix ".tchadDev"
            buildConfigField "String", "API_BASE_URL", '"http://imis-tchad-dev.swisstph-mis.ch/rest/"'
            buildConfigField "String", "APP_DIR", '"IMIS-TCHADDEV"'
            resValue "string", "app_name_claims", "Claims TCHAD DEV"
            dimension 'std'
        }
        local {
            applicationIdSuffix ".local"
            buildConfigField "String", "APP_DIR", '"IMIS-LOCAL"'
            resValue "string", "app_name_claims", "Claims Local"
            buildConfigField "String", "API_BASE_URL", '"http://10.0.2.2:35787/"'
            dimension 'std'
        }
        cli {
            applicationIdSuffix System.getenv("APPLICATION_ID_SUFFIX") ?: ".cli"
            buildConfigField "String", "APP_DIR", '"' + (System.getenv("CLI_APP_DIR") ?: 'IMIS-CLI') + '"'
            resValue "string", "app_name_claims", System.getenv("CLI_APP_NAME") ?: "Claims CLI"
            buildConfigField "String", "API_BASE_URL", '"' + (System.getenv("API_BASE_URL") ?: 'http://10.0.2.2:35787/') + '"'
            dimension 'std'
        }
        mauritaniaTrain {
            applicationIdSuffix ".mauritaniaTrain"
            buildConfigField "String", "API_BASE_URL", '"http://formation.cnass-mauritanie.swisstph-mis.ch/rest/"'
            buildConfigField "String", "APP_DIR", '"IMIS-MAURITANIA-TRAIN"'
            resValue "string", "app_name_claims", "Claims CNASS TRAIN"
            dimension = 'std'
        }

    }

    sourceSets {
        demoProd.java.srcDir 'src/demoProd/java'
        demoProd.res.srcDir 'src/demoProd/res'

        demoRelease.java.srcDir 'src/demoRelease/java'
        demoRelease.res.srcDir 'src/demoRelease/res'

        chfDev.java.srcDir 'src/chfDev/java'
        chfDev.res.srcDir 'src/chfDev/res'

        mvDev.java.srcDir 'src/mvDev/java'
        mvDev.res.srcDir 'src/mvDev/res'

        bephaDev.java.srcDir 'src/bephaDev/java'
        bephaDev.res.srcDir 'src/bephaDev/res'

        niger.java.srcDir 'src/niger/java'
        niger.res.srcDir 'src/niger/res'

        tchadDev.java.srcDir 'src/tchad/java'

        local.java.srcDir 'src/local/java'
        local.res.srcDir 'src/local/res'

        mauritaniaTrain.java.srcDir 'src/demoProd/java'

        cli.java.srcDir System.getenv("CLI_JAVA_DIR") ?: 'src/demoProd/java'
        cli.res.srcDir System.getenv("CLI_RES_DIR") ?: 'src/demoProd/res'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

if(file('custom-flavours.gradle').exists()){
    apply from: 'custom-flavours.gradle'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.embarkmobile:zxing-android-minimal:1.2.1@aar'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation group: 'com.squareup.picasso', name: 'picasso', version: '2.5.2'
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '1.2.7'

}
